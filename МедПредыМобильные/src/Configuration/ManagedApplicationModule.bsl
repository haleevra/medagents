// ПодключаемоеОборудование
Перем глПодключаемоеОборудование Экспорт; // для кэширования на клиенте
// Конец ПодключаемоеОборудование

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередНачаломРаботыСистемы(Отказ)
	
	ПользовательСоздан = Ложь;
	ОбщегоНазначенияВызовСервера.ОбработатьЗапускПриложения(Отказ, ПользовательСоздан);
	ОбщегоНазначенияВызовСервера.ОбновитьПриложение();
	
	Если ПользовательСоздан Тогда
		Сообщить("Пользователь создан успешно");
		Отказ = Истина;
		ЗавершитьРаботуСистемы(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
		
	ОбщегоНазначенияКлиент.ПолучитьИдентификаторПодписчика();
	
	Если ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("КодДоступа") <> "" Тогда
		Результат = ОткрытьФормуМодально("ОбщаяФорма.ВводКодаДоступаВПриложение");
		Если Результат = Неопределено ИЛИ НЕ Результат Тогда
			ЗавершитьРаботуСистемы(Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СчетчикОткрытияФормыОценкиПриложения = ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("СчетчикОткрытияФормыОценкиПриложения");
	Если СчетчикОткрытияФормыОценкиПриложения = Неопределено Тогда
		СчетчикОткрытияФормыОценкиПриложения = 0;
	КонецЕсли;
	Если СчетчикОткрытияФормыОценкиПриложения >= 5 Тогда
		ОткрытьФормуМодально("ОбщаяФорма.ОставитьОтзыв", Новый Структура("ПоказатьНеСпрашивать", Истина));
	ИначеЕсли СчетчикОткрытияФормыОценкиПриложения <> -1 Тогда
		СчетчикОткрытияФормыОценкиПриложения = СчетчикОткрытияФормыОценкиПриложения + 1;
		ОбщегоНазначенияВызовСервера.УстановитьЗначениеКонстанты("СчетчикОткрытияФормыОценкиПриложения", СчетчикОткрытияФормыОценкиПриложения);
	Конецесли;
	
	ОбменМобильноеПриложениеФоновыеЗадания.УстановитьКонстантыПриСтарте();
		
	#Если МобильноеПриложениеКлиент Тогда
//	ДоставляемыеУведомления.ПодключитьОбработчикУведомлений("ПриПолученииУведомления");
//	Если СредстваТелефонии.ПоддерживаетсяОбработкаЗвонков() Тогда
//		СредстваТелефонии.ПодключитьОбработчикЗвонков("Подключаемый_ОбработатьЗвонок");
//	КонецЕсли;
	#КонецЕсли
		
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьНеобходимостьПерезапуска", 5, Ложь);
	ОбменМобильноеПриложениеФоновыеЗадания.ВыполнениеОбмена();
	
КонецПроцедуры

Процедура Подключаемый_ПроверитьНеобходимостьПерезапуска() Экспорт
	
	Если ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ОповеститьОВыполненииОбмена") Тогда
		ОбщегоНазначенияВызовСервера.УстановитьЗначениеКонстанты("ОповеститьОВыполненииОбмена", Ложь);
		Оповестить("ПрошелОбмен");
	КонецЕсли;
	
	Если ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ПерезапуститьПриложение") Тогда
		ОбщегоНазначенияВызовСервера.УстановитьЗначениеКонстанты("ПерезапуститьПриложение", Ложь);
		Предупреждение(НСтр("ru='На сервере изменились права доступа, требуется перезапуск приложения.'"));
		ЗавершитьРаботуСистемы(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура Подключаемый_ОбработатьЗвонок(НомерТелефона, Дата, ВариантСобытия, ТипЗвонка) Экспорт
	
	#Если МобильноеПриложениеКлиент Тогда
		
	Если ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.ЗавершениеВходящего Тогда
		ВариантСобытияСтрокой = "ЗавершениеВходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.ЗавершениеИсходящего Тогда
		ВариантСобытияСтрокой = "ЗавершениеИсходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.НачалоВходящего Тогда
		ВариантСобытияСтрокой = "НачалоВходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.НачалоИсходящего Тогда
		ВариантСобытияСтрокой = "НачалоИсходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.НачалоСигналаВходящего Тогда
		ВариантСобытияСтрокой = "НачалоСигналаВходящего";
	Конецесли;

	ОбменМобильноеПриложениеСервер.ОтправитьИнформациюОЗвонкеНаСервер(НомерТелефона, Дата, ВариантСобытияСтрокой, ТипЗвонка);
	
	#КонецЕсли
	
КонецПроцедуры
