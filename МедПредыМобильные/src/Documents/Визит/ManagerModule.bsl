
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = НСтр("en='Visit No.';ru='Визит №'") + ОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеНомера(Данные.Номер) + НСтр("ru=' от ';en=' of '") + Формат(Данные.Дата, "ДФ=dd.MM.yyyy");
	
КонецПроцедуры

Функция ПолучитьКоличествоВизитовВСостоянии(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(Визит.Ссылка) КАК КоличествоЗаказов
	|ИЗ
	|	Документ.Визит КАК Визит
	|ГДЕ
	|	НЕ Визит.ПометкаУдаления
	|	И Визит.Проект = &Проект
	|	И Визит.Проведен
	|	И НАЧАЛОПЕРИОДА(Визит.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	
	ВыборкаЗаказов = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ВыборкаЗаказов.Следующий() Тогда
		Возврат ВыборкаЗаказов.КоличествоЗаказов;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция СформироватьПечатнуюФорму(Ссылка) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Визит.Ссылка,
	|	Визит.ВерсияДанных,
	|	Визит.ПометкаУдаления,
	|	Визит.Номер,
	|	Визит.Дата,
	|	Визит.Проведен,
	|	Визит.Контрагент,
	|	Визит.СостояниеВизита,
	|	Визит.Отгружен,
	|	Визит.Оплачен,
	|	Визит.СуммаДокумента,
	|	Визит.ИзЦентральнойБазы,
	|	Визит.ОтгруженУстановленВЦентральнойБазе,
	|	Визит.ОплаченУстановленВЦентральнойБазе,
	|	Визит.Комментарий,
	|	Визит.СуммаОплаты,
	|	Визит.СуммаСкидки,
	|	Визит.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Товар,
	|		Цена,
	|		Количество,
	|		Сумма
	|	)
	|ИЗ
	|	Документ.Визит КАК Визит
	|ГДЕ
	|	Визит.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Заказ = Запрос.Выполнить().Выбрать();
	Заказ.Следующий();

	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	Макет = Документы.Визит.ПолучитьМакет("СчетНаОплату");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.ТекстЗаголовка =
		НСтр("ru='Счет на оплату №';en='INVOICE #'")
		+ Заказ.Номер
		+ НСтр("ru=' от ';en=' DATE '")
		+ Формат(Заказ.Дата, "ДЛФ=DD");
	
	ОбластьМакета.Параметры.ПредставлениеПоставщика = Константы.НазваниеОрганизации.Получить();
	ОбластьМакета.Параметры.РеквизитыДляОплаты = Константы.ПлатежныеРеквизиты.Получить();
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ВыборкаСтрокТовары = Заказ.Товары.Выбрать();
	
	Количество = 0;
	
	Пока ВыборкаСтрокТовары.Следующий() Цикл
		Количество = Количество + 1;
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.НомерСтроки = Количество;
		ОбластьМакета.Параметры.Товар = ВыборкаСтрокТовары.Товар.Наименование;
		ОбластьМакета.Параметры.Количество = ВыборкаСтрокТовары.Количество;
		ОбластьМакета.Параметры.Цена = ВыборкаСтрокТовары.Цена;
		ОбластьМакета.Параметры.Сумма = ВыборкаСтрокТовары.Сумма;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если Заказ.СуммаСкидки <> 0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоСкидка");
		ОбластьМакета.Параметры.ВсегоБезСкидки = Заказ.СуммаДокумента + Заказ.СуммаСкидки;
		ОбластьМакета.Параметры.СуммаСкидки = Заказ.СуммаСкидки;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоКОплате");
	ОбластьМакета.Параметры.ИтогоКОплате = Заказ.СуммаДокумента;
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если НСтр("ru='ru';en='en'") = "ru" Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ПодвалСчета");
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции