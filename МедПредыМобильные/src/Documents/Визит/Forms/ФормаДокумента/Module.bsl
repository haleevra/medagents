&НаКлиенте
Перем КэшНайденныхКонтактов;

#Область ПроцедурыИФункцииОбщегоНазначения

&НаСервере
Процедура СформироватьФормуВизита()
	
	Если Объект.Операция = ПредопределенноеЗначение("Перечисление.ПрофилиМобильногоПриложения.МедПредставитель") Тогда
		Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
			Если ЭтоАптека() Тогда
				СформироватьТоварыИЦены();
			Иначе
				СформироватьЧекЛист();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	УстановитьВидимостьСобытия();
	
КонецПроцедуры

&НаСервере
Функция ЭтоАптека()
	
	ЭтоАптека = (Объект.Контрагент.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.Аптека"));
	Возврат ЭтоАптека;

КонецФункции

&НаСервере
Процедура СформироватьЧекЛист()
	
	Объект.Препараты.Очистить();
	Объект.Цены.Очистить();
	
	Для Каждого ПунктЧекЛиста Из Объект.Проект.ЧекЛист Цикл
		НовыйПункт = Объект.ЧекЛист.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйПункт, ПунктЧекЛиста);
		НовыйПункт.Значение = ПустоеЗначение(НовыйПункт.ТипЗначения);		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьТоварыИЦены()
	
	Объект.ЧекЛист.Очистить();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПроектыПрепараты.Товар
	|ИЗ
	|	Справочник.Проекты.Препараты КАК ПроектыПрепараты
	|ГДЕ
	|	ПроектыПрепараты.Ссылка = &Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроектыПрепараты.Товар,
	|	ЛОЖЬ КАК Конкурент
	|ИЗ
	|	Справочник.Проекты.Препараты КАК ПроектыПрепараты
	|ГДЕ
	|	ПроектыПрепараты.Ссылка = &Проект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПроектыКонкуренты.Товар,
	|	ИСТИНА
	|ИЗ
	|	Справочник.Проекты.Конкуренты КАК ПроектыКонкуренты
	|ГДЕ
	|	ПроектыКонкуренты.Ссылка = &Проект
	|
	|УПОРЯДОЧИТЬ ПО
	|	Конкурент"
	;
	Запрос = Новый Запрос(ТекстЗапроса);

	Запрос.УстановитьПараметр("Проект", Объект.Проект);
	
	Результаты = Запрос.ВыполнитьПакет();
	Объект.Препараты.Загрузить(Результаты[0].Выгрузить());
	Объект.Цены.Загрузить(Результаты[1].Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСобытия(ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = Объект;
	КонецЕсли;
	
	Если ТекущийОбъект.Операция = Перечисления.ПрофилиМобильногоПриложения.Супервайзер Тогда
		Элементы.Супервайзер.Видимость = Истина;
		Элементы.МедПредставитель.Видимость = Ложь;
		Элементы.Ответственный.Видимость = Истина;
	Иначе
		Если ЗначениеЗаполнено(ТекущийОбъект.Контрагент) Тогда
			ЭтоАптека = ЭтоАптека();
			
			Элементы.СтраницаЧекЛист.Видимость = НЕ ЭтоАптека;
			Элементы.СтраницаПрепараты.Видимость = ЭтоАптека;
			Элементы.СтраницаЦены.Видимость = ЭтоАптека;
			
			Если ЭтоАптека Тогда
				Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Пролистывание;
			Иначе
				Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ТекущийОбъект.СостояниеВизита = Перечисления.СостоянияВизитов.Закрыт Тогда
		Если НЕ Константы.ПрофильМобильногоПриложения.Получить() = Перечисления.ПрофилиМобильногоПриложения.Супервайзер Тогда
			Элементы.СостояниеВизита.СписокВыбора.Удалить(3);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьФорму(Заблокировать = Ложь)
	Элементы.ГруппаКолонки.Доступность = Заблокировать;
	Элементы.ВидВизита.Доступность = Заблокировать;
	Элементы.Прокрутка.Доступность = Заблокировать;
	//Элементы.ГруппаКоординаты.Доступность = Заблокировать;
	//Элементы.Ответственный.Доступность = Заблокировать;
КонецПроцедуры

#КонецОбласти

#Область ДействияКомандныхПанелейФормы

&НаКлиенте
Процедура Справка(Команда)
	
	ПерейтиПоНавигационнойСсылке("http://server.prof-it.ru/redmine/projects/dfgwiki/wiki/Wiki");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если МобильноеПриложениеКлиент Тогда
		Если Не ЗначениеЗаполнено(Объект.ДатаНачало) Тогда
			ЗаписатьГеоДанные(МедПредыГеолокация.ПолучитьКоординаты(), "Начало");
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция ПустоеЗначение(ТипЗначения)
	Если ТипЗначения = "Строка" Тогда
		Возврат "";
	ИначеЕсли ТипЗначения = "Число" Тогда
		Возврат 0;
	ИначеЕсли ТипЗначения = "Дата" Тогда
		Возврат Дата(1,1,1);
	ИначеЕсли ТипЗначения = "Булево" Тогда
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	//ОбщегоНазначенияСервер.УстановитьЗаголовокФормы(ЭтаФорма, НСтр("ru='Визит';en='Visit'"));
	
	УстановитьВидимостьСобытия(ТекущийОбъект);
	ЭтаФорма.ТолькоПросмотр = (ТекущийОбъект.СостояниеВизита = Перечисления.СостоянияВизитов.Закрыт) И Объект.Проведен;
	
	ФотоАдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(Новый Картинка(ТекущийОбъект.Картинка.Получить()));
	
	Если ТекущийОбъект.СостояниеВизита = Перечисления.СостоянияВизитов.Выполнен Тогда
		ЗаблокироватьФорму();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзменилсяВизит", Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СозданНовыйКонтрагент" Тогда
		Объект.Контрагент = Параметр;
		ЭтаФорма.Модифицированность = Истина;
	КонецЕСли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	МенеджерКонтактовКлиент.ЗаполнитьСписокВыбораИзКонтактнойКниги(
	Текст,
	ДанныеВыбора,
	СтандартнаяОбработка,
	КэшНайденныхКонтактов,
	Истина
	);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	МенеджерКонтактовКлиент.ОбработкаВыбора(
	Элемент,
	ВыбранноеЗначение,
	СтандартнаяОбработка,
	ЭтаФорма,
	КэшНайденныхКонтактов
	);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	 СформироватьФормуВизита();
	 
КонецПроцедуры

&НаСервере
Процедура ЗаписатьГеоДанные(ДанныеМестоположения, Момент)
	
	//ОбъектДляЗаписи = РеквизитФормыВЗначение("Объект");
	Объект["Дата"+Момент]       = ТекущаяДата();
	Попытка
		ЭтаФорма["ГеоМетка"+Момент] = ПоместитьВоВременноеХранилище(Новый ХранилищеЗначения(ДанныеМестоположения.Координаты), УникальныйИдентификатор);
	Исключение
		Сообщить("Местоположение неопределено!", СтатусСообщения.ОченьВажное);
	КонецПопытки;
	//ОбъектДляЗаписи.Записать(РежимЗаписиДокумента.Запись);
	//ЗначениеВРеквизитФормы(ОбъектДляЗаписи, "Объект");

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	#Если МобильноеПриложениеКлиент Тогда
		Попытка
			Если Не ЗначениеЗаполнено(Объект.ДатаОкончание) Тогда
				ЗаписатьГеоДанные(МедПредыГеолокация.ПолучитьКоординаты(), "Окончание");
			КонецЕсли;
		Исключение
			Инфо = ИнформацияОбОшибке();
			Текст=Символы.ПС+НСтр("ru='Описание=';en='Description='") + Инфо.Описание + "'";
			Текст=Текст+Символы.ПС+НСтр("ru='ИмяМодуля=';en='ModuleName='") + Инфо.ИмяМодуля + "'";
			Текст=Текст+Символы.ПС+НСтр("ru='НомерСтроки=';en='LineNumber='") + Инфо.НомерСтроки + "'";
			Текст=Текст+Символы.ПС+НСтр("ru='ИсходнаяСтрока=';en='SourceLine='") + Инфо.ИсходнаяСтрока + "'";
					
			СлужебныйКомментарий = СлужебныйКомментарий + Текст;
		КонецПопытки;
	#КонецЕсли
КонецПроцедуры


&НаСервере
Функция ПередЗакрытиемНаСервере()
	Если ЭтаФорма.Модифицированность Тогда
		ОбъектДокумента = РеквизитФормыВЗначение("Объект");
		Возврат НЕ ОбъектДокумента.ПроверитьЗаполнение();
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = ПередЗакрытиемНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	ЭтаФорма.Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ЭтоНовый() Тогда
		Попытка
			ТекущийОбъект.ГеоМеткаНачало = ПолучитьИзВременногоХранилища(ЭтаФорма.ГеоМеткаНачало);
			ТекущийОбъект.ГеоМеткаОкончание = ПолучитьИзВременногоХранилища(ЭтаФорма.ГеоМеткаОкончание);
		Исключение
			Инфо = ИнформацияОбОшибке();
			ТекущийОбъект.СлужебныйКомментарий = ТекущийОбъект.СлужебныйКомментарий + СлужебнаяИнформация(Инфо);
		КонецПопытки;
	КонецЕсли;
	Если ЗначениеЗаполнено(ФотоАдресФайлаВоВременномХранилище) Тогда
		Попытка
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(ФотоАдресФайлаВоВременномХранилище);
			ТекущийОбъект.Картинка = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		Исключение
			Инфо = ИнформацияОбОшибке();
			ТекущийОбъект.СлужебныйКомментарий = ТекущийОбъект.СлужебныйКомментарий + СлужебнаяИнформация(Инфо);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

// Описание
// 
// Параметры:
// 	ТекущийОбъект - Описание
&НаСервере
Функция СлужебнаяИнформация(Инфо)
	Текст=Символы.ПС+НСтр("ru='Описание=';en='Description='") + Инфо.Описание + "'";
	Текст=Текст+Символы.ПС+НСтр("ru='ИмяМодуля=';en='ModuleName='") + Инфо.ИмяМодуля + "'";
	Текст=Текст+Символы.ПС+НСтр("ru='НомерСтроки=';en='LineNumber='") + Инфо.НомерСтроки + "'";
	Текст=Текст+Символы.ПС+НСтр("ru='ИсходнаяСтрока=';en='SourceLine='") + Инфо.ИсходнаяСтрока + "'";
	Возврат Текст;
КонецФункции


&НаКлиенте
Процедура СостояниеВизитаПриИзменении(Элемент)
	Если (Объект.СостояниеВизита = ПредопределенноеЗначение("Перечисление.СостоянияВизитов.Выполнен"))
	   ИЛИ (Объект.СостояниеВизита = ПредопределенноеЗначение("Перечисление.СостоянияВизитов.Закрыт")) Тогда
		ЗаблокироватьФорму();
	Иначе
		ЗаблокироватьФорму(Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	Если ТекущийОбъект.ЭтоНовый() Тогда
		Если Константы.ПрофильМобильногоПриложения.Получить() = Перечисления.ПрофилиМобильногоПриложения.Супервайзер Тогда
			ТекущийОбъект.Операция =  Перечисления.ПрофилиМобильногоПриложения.Супервайзер;
		КонецЕсли;
		ТекущийОбъект.Проект = Константы.ТекущийПроект.Получить(); 
		Если НЕ ЗначениеЗаполнено(ТекущийОбъект.Проект) Тогда
			Сообщить("Не задан проект на рабочем столе!");
			Отказ = Истина;
		КонецЕсли;
		ТекущийОбъект.Контрагент = Константы.ТекущийКонтрагент.Получить(); 		
		Если НЕ ЗначениеЗаполнено(ТекущийОбъект.Контрагент) Тогда
			Сообщить("Не задан контрагент на рабочем столе!");
			Отказ = Истина;
		КонецЕсли;
		ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
		СформироватьФормуВизита();
	КонецЕсли;
	
	//ОбщегоНазначенияСервер.УстановитьЗаголовокФормы(ЭтаФорма, НСтр("ru='Визит';en='Visit'"));
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	КонтактноеЛицоПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КонтактноеЛицоПриИзмененииНаСервере()

	Если Объект.Операция = Перечисления.ПрофилиМобильногоПриложения.МедПредставитель Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Визит.Ссылка
		|ИЗ
		|	Документ.Визит КАК Визит
		|ГДЕ
		|	Визит.Проект = &Проект
		|	И Визит.Контрагент = &Контрагент
		|	И Визит.КонтактноеЛицо = &КонтактноеЛицо
		|	И Визит.Дата >= ДОБАВИТЬКДАТЕ(&Дата, ДЕНЬ, -&Цикличность)
		|	И Визит.Операция = ЗНАЧЕНИЕ(Перечисление.ПрофилиМобильногоПриложения.МедПредставитель)"
		;
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДата()));
		Запрос.УстановитьПараметр("Проект", Объект.Проект);
		Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);	
		Запрос.УстановитьПараметр("КонтактноеЛицо", Объект.КонтактноеЛицо);
		Запрос.УстановитьПараметр("Цикличность", Объект.Проект.Цикличность);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Сообщить(СтрШаблон("С предыдущего визита не прошло требуемое количество дней %1", Объект.Проект.Цикличность), СтатусСообщения.Внимание);
			Объект.КонтактноеЛицо = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДобавитьФотоНажатие(Элемент)
	#Если МобильноеПриложениеКлиент Тогда
		Фото = СредстваМультимедиа.СделатьФотоснимок(, Новый РазрешениеКамерыУстройства(1280, 720), ?(ОбщегоНазначенияВызовСервера.ВерсияОС()="iOS", 50, 100));
		Если Фото <> Неопределено Тогда
			ФотоАдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(Фото.ПолучитьДвоичныеДанные(), УникальныйИдентификатор);
		КонецЕсли;
	#КонецЕсли
КонецПроцедуры

#КонецОбласти