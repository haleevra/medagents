#Область СлужебныйПрограммныйИнтерфейс

// Получает массив контрагентов по строке поиска.
//
Функция ПолучитьМассивДанныхВыбораСправочника(СтрокаПоиска) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	Контрагенты.Наименование,
	|	ВЫБОР
	|		КОГДА Контрагенты.Наименование ПОДОБНО &СтрокаПоиска + ""%""
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ПометкаУдаления
	|	И (Контрагенты.Наименование ПОДОБНО &СтрокаПоиска + ""%""
	|			ИЛИ Контрагенты.Наименование ПОДОБНО ""% "" + &СтрокаПоиска + ""%"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Контрагенты.Наименование";
	
	Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска);
	ТаблицаРезультатЗапроса = Запрос.Выполнить().Выгрузить();
	МассивСсылок = ТаблицаРезультатЗапроса.ВыгрузитьКолонку("Ссылка");
	Возврат МассивСсылок;
	
КонецФункции

// Добавляет нового контрагента.
//
Функция ДобавитьКонтрагента(Данные) Экспорт
	
	НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
	ЗаполнитьРеквизитыКонтрагента(НовыйКонтрагент, Данные);
	НовыйКонтрагент.Записать();
	Возврат НовыйКонтрагент.Ссылка;
	
КонецФункции

// Заполняет реквизиты контрагента.
//
Процедура ЗаполнитьРеквизитыКонтрагента(Контрагент, ВыбранноеЗначение) Экспорт
	
	Контрагент.Наименование = ВыбранноеЗначение[0].Имя +
	?(ЗначениеЗаполнено(ВыбранноеЗначение[0].Отчество), " " + ВыбранноеЗначение[0].Отчество, "") +
	?(ЗначениеЗаполнено(ВыбранноеЗначение[0].Фамилия)," "+ ВыбранноеЗначение[0].Фамилия,"") +
	?(ЗначениеЗаполнено(ВыбранноеЗначение[0].Организация)," (" + ВыбранноеЗначение[0].Организация + ")", "");
	
	Для каждого ДанныеКонтакта из ВыбранноеЗначение Цикл
		
		Сч = 0;
		Для каждого ЭлементДанных Из ДанныеКонтакта.НомераТелефонов Цикл
			Сч = Сч + 1;
			Если Сч = 1 Тогда
				Контрагент.НомерТелефона = ЭлементДанных.Значение;
			Иначе
				Контрагент.Комментарий = Контрагент.Комментарй + НСтр("ru='доп. тел: ';en='tel: '") + ЭлементДанных.Значение + Символы.ПС;
			КонецЕсли;
		КонецЦикла;
		
		Сч = 0;
		Для каждого ЭлементДанных Из ДанныеКонтакта.Адреса Цикл
			Сч = Сч + 1;
			Если Сч = 1 Тогда
				Контрагент.Адрес = ЭлементДанных.Значение;
			Иначе
				Контрагент.Комментарий = Контрагент.Комментарй + НСтр("ru='доп. адрес: ';en='adress: '") + ЭлементДанных.Значение + Символы.ПС;
			КонецЕсли;
		КонецЦикла;
		
		Сч = 0;
		Для каждого ЭлементДанных Из ДанныеКонтакта.АдресаЭлектроннойПочты Цикл
			Сч = Сч + 1;
			Если Сч = 1 Тогда
				Контрагент.АдресЭП = ЭлементДанных.Значение;
			Иначе
				Контрагент.Комментарий = Контрагент.Комментарй + НСтр("ru='доп. email: ';en='email: '") + ЭлементДанных.Значение + Символы.ПС;
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ЭлементДанных из ДанныеКонтакта.ВебАдреса Цикл
			Контрагент.Комментарий = Контрагент.Комментарй + "WEB: " + ЭлементДанных.Значение + Символы.ПС;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
